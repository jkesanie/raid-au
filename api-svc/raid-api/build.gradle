import org.gradle.api.plugins.jvm.JvmTestSuite
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'java'
  id 'jvm-test-suite'
  alias(libs.plugins.spring.boot)
  alias(libs.plugins.jsonschema2pojo)
  alias(libs.plugins.xjc)
  id 'io.spring.dependency-management' version '1.1.6'
}

//apply plugin: 'docker-compose'

ext {
  defaultSysProps = [
    /* prod machines will always be set to UTC, but developer machines are set
    to whatever they want - so this forces it for gradle stuff. */
    'user.timezone': 'UTC',
    // platform specific, so force it
    'file.encoding': 'UTF-8',
    // for consistency, rather than any specific reason
    "user.language":"", "user.country":"", "user.variant":"",
  ]

  // the config loading config :/
  homeDir = System.properties['user.home']
  configPath = System.getProperty("RAIDO_APISVC_CONFIG_PATH",
    "${homeDir}/.config/raido/api-svc.gradle")
}

/* after ext{} block so that stuff is the default, and so the config
   can refer to those default values */
if( file(configPath).exists() ){
  println "loading config from: ${configPath}"
  /* never done config this way before, not entirely sure it's a good idea,
  probably a Bad Plan to use a full turing-complete language for config */
  apply from: configPath
}

version = rootProject.version

/* don't use groovy for prod code: dynamic typing, slow startup and poor
   historical upgrade compatibility */
dependencies{
  compileOnly libs.annotations
  implementation project(':api-svc:db')
  implementation project(':api-svc:idl-raid-v2')
  implementation libs.flyway.core
  // look in settings.gradle for version catalog specs
  implementation libs.bundles.springboot
  implementation libs.springdoc.openapi

  implementation 'jakarta.annotation:jakarta.annotation-api'

  implementation libs.geonames.ws.client
  compileOnly libs.lombok
  annotationProcessor libs.lombok

  testCompileOnly libs.lombok
  testAnnotationProcessor libs.lombok
    // postgres DB driver
  runtimeOnly libs.postgresql

  runtimeOnly libs.jackson.databind
  implementation libs.jackson.dataformat.xml
  implementation libs.jackson.datatype.jsr310
  implementation libs.jackson.annotations

  implementation libs.jakarta.json.api
  implementation libs.jakarta.json

  implementation libs.jakarta.xml.bind.api
  implementation libs.jaxb.runtime
  
  // RDF libraries for content negotiation
  implementation 'org.apache.jena:jena-core:4.8.0'
  implementation 'org.apache.jena:jena-arq:4.8.0'

  testImplementation libs.bundles.springboot.test

  testImplementation testFixtures(project(':api-svc:testFixtures'))

  testImplementation libs.mockito.inline
  testImplementation libs.mockito.junit.jupiter
  testImplementation libs.junit.jupiter

  testImplementation libs.assertj

  // because MockRestServiceServer couldn't find class org.hamcrest.Matcher
  testImplementation libs.hamcrest

  // needed for jsonpath testing in Spring MVC tests
  testImplementation libs.spring.security.test

  // XJC dependencies - need the actual compiler tools
  xjc libs.jaxb.xjc
  xjc libs.jaxb.runtime
  xjc libs.jakarta.xml.bind.api
}

compileJava {
  options.compilerArgs <<
    // avoid warnings about spring classes using deprecated '-debug'
    "-parameters" <<
    // annoy people into not doing unchecked shenanigans ðŸ˜’
    "-Xlint:unchecked" <<
    // annoy people into fixing their deprecation warnings
    "-Xlint:deprecation"
}

testing{
  suites{
    intTest(JvmTestSuite){
      dependencies{
        implementation project(':api-svc:idl-raid-v2')

        implementation.bundle libs.bundles.springboot
        implementation.bundle libs.bundles.springboot.test

        implementation testFixtures(project(':api-svc:testFixtures'))
        implementation.bundle libs.bundles.feign

        implementation libs.assertj

        // for PostConstruct/PreDestroy - pkg: jakarta.annotation (used to be javax)
        implementation libs.jakarta.annotation.api

        implementation libs.jackson.datatype.jsr310

        compileOnly libs.lombok
        annotationProcessor libs.lombok

      }
    }
  }
}
tasks.intTest.description = "don't run this, it's just a Gradle default"

tasks.register('buildInfo', WriteProperties) {
    description = "Write build-info.properties file"
    destinationFile = file("${sourceSets.main.output.resourcesDir}/META-INF/build-info.properties")
    encoding = "UTF-8"
    comment = "This file built by Gradle task buildInfo"

    property("build.version", project.version)
//  property("build.commitId", rootProject.gitCommitHash)
    // we don't use it for stuff, it's for people, so format it nice
    property("build.buildDate", new Date().toString())
}
processResources.dependsOn buildInfo

bootJar {
  archiveFileName = "raido-api-svc.jar"
}

bootBuildImage {
  imageName = 'raid-api-base:latest'
}

tasks.register('buildDocker', Exec) {
  dependsOn bootBuildImage
  def imageName = project.hasProperty('imageName') ? project.imageName : 'raid-api:latest'
  commandLine 'docker', 'build', '-t', imageName, '.'
}

tasks.register('dockerComposeUp', Exec) {
  dependsOn ':iam:build'
  commandLine 'docker', 'compose', 'up', '--wait', '--detach'
}

tasks.register('dockerComposeDown', Exec) {
  commandLine 'docker', 'compose', 'down', '-v'
}

tasks.withType(Test) {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    showExceptions = true
    exceptionFormat = TestExceptionFormat.FULL
    showCauses = true
    showStackTraces = true
  }
}

jsonSchema2Pojo {
  // Source directory containing your JSON schema files
  source = files("${projectDir}/src/main/resources/ror_schema_v2_1.json")

  // Target directory for generated Java classes
  targetDirectory = file("${projectDir}/src/main/generated")

  // Target package for generated classes
  targetPackage = 'au.org.raid.api.client.ror.dto'

  // Include generated sources in compilation
  sourceType = 'jsonschema'

  // Generate builder methods
  generateBuilders = true

  // Include JSR-303 annotations (@Valid, @NotNull, etc.)
  useJakartaValidation = true
  includeJsr303Annotations = true

  // Jackson annotations are already included by default (annotationStyle = 'jackson')

  // Date/time type configuration
  dateTimeType = 'java.time.OffsetDateTime'

  // Additional options
  useLongIntegers = false
  useDoubleNumbers = true
  includeHashcodeAndEquals = true
  includeToString = true
  includeAllPropertiesConstructor = true
  constructorsRequiredPropertiesOnly = false
}

sourceSets {
  main {
    java {
      srcDir "${projectDir}/src/main/generated"
    }
  }
}

tasks.named('compileJava') {
  dependsOn tasks.named('generateJsonSchema2Pojo')
}

xjc {
  xsdDir.set(layout.projectDirectory.dir("src/main/resources/schema/xsd"))
  bindingFiles.from("src/main/resources/schema/xsd/bindings.xjb")
  outputJavaDir.set(layout.projectDirectory.dir("src/main/generated"))
  defaultPackage.set("au.org.raid.api.client.isni.dto")
}

tasks.named('compileJava') {
  dependsOn tasks.named('generateJsonSchema2Pojo')
  dependsOn tasks.named('xjc')
}

bootRun {
  systemProperties = System.properties
}
